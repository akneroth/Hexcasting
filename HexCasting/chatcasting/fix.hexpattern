#git(/HexCasting/libs/base.hexpattern)

// get amount from param
$runif(
    Flock's Reflection
    Augur's Purification
)(
    $if(
        Gemini Decomposition
        Length Purification
        Numerical Reflection: 1
        Equality Distillation
    )(
        // if exactly 1 parameter was passed
        $if(
            Flock's Disintegration
            Input Purification
            Gemini Decomposition
            // check if it is a proper number
            Augur's Purification
        )(
            // use passed number
        )(
            Bookkeeper's Gambit: v
        )
    )(
        Bookkeeper's Gambit: v
    )
)

// main hand
$if(
    Flock's Reflection
    Augur's Purification
)()(
    $if(
        Mind's Reflection
        Duelist's Purification
        Length Purification
        Augur's Purification
    )(
        // main hand is not empty
        $runif(
            Mind's Reflection
            Tool Purification
            Fragility Purification

            Gemini Decomposition
            Augur's Purification
        )(
            // main hand max duability is non 0
            $print(Using main hand durability)
        )(
            // main hand max duability is 0
            Bookkeeper's Gambit: v
        )
    )()
)

// off hand
$if(
    Flock's Reflection
    Augur's Purification
)()(
    $if(
        Mind's Reflection
        Shieldbearer's Purification
        Length Purification
        Augur's Purification
    )(
        // off hand is not empty
        $runif(
            Mind's Reflection
            Accessory Purification
            Fragility Purification

            Gemini Decomposition
            Augur's Purification
        )(
            // off hand max duability is non 0
            $print(Using off hand durability)
        )(
            // off hand max duability is 0
            Bookkeeper's Gambit: v
        )
    )()
)

// no durability items
$if(
    Flock's Reflection
    Augur's Purification
)()(
    $print(Using default amount of media)
    Numerical Reflection: 640
)

// print amount of media
Reveal



// get amount
Numerical Reflection: 5
Division Distillation
Ceiling Purification


// get caster foot pos
Mind's Reflection
Compass' Purification II

// get mote
$if(
    {@item_type(minecraft:amethyst_shard, true)} >>
    Stocktake Purification
    Gemini Decomposition
    Augur's Purification
)(
    Numerical Reflection: 0
    Selection Distillation
)(
    $print(No amethyst shards in binded mote nexus)
    Janus' Gambit
)

// get items from mote
Reflecting Gambit
Return Item

// repair using amethyst on the ground
$foreach(
    // scan for items on ground
    Mind's Reflection
    Compass' Purification II
    Numerical Reflection: 1
    Zone Distillation: Item
)(
    $if(
        Gemini Decomposition
        Sorter's Purification
        {@item_type(minecraft:amethyst_shard, true)} >>
        Equality Distillation
    )(
        {
            // handle repair if is amethyst shard
            Induce Mending
            $print(Repaired item's durability with media)
        }
        Athena's Gambit
        Bookkeeper's Gambit: v
        {
            // handle refill if is amethyst shard
            Recharge Item
            $print(Recharged item with media)
        }
        Athena's Gambit
        Bookkeeper's Gambit: v
    )()
)
Bookkeeper's Gambit: v


// return left amethyst
$foreach(
    // scan for items on ground
    Mind's Reflection
    Compass' Purification II
    Numerical Reflection: 1
    Zone Distillation: Item
)(
    $if(
        Gemini Decomposition
        Sorter's Purification
        {@item_type(minecraft:amethyst_shard, true)} >>
        Equality Distillation
    )(
        Mediafy Item
    )()
)
Bookkeeper's Gambit: v
