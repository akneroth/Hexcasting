#git(/HexCasting/libs/base.hexpattern)

// get amount from param
Whisper Reflection
Input Purification
Gemini Decomposition
Augur's Purification
$fastif(
    $print(Using last number sent to chat via hex whisper)
    // use passed number
)(
    Bookkeeper's Gambit: v
)

// main hand
$if(
    Flock's Reflection
    Augur's Purification
)()(
    $if(
        Mind's Reflection
        Duelist's Purification
        Length Purification
        Augur's Purification
    )(
        // main hand is not empty
        $if(
            Mind's Reflection
            Tool Purification
            Fragility Purification

            Gemini Decomposition
            Augur's Purification
        )(
            // main hand max duability is non 0
            Menderbug's Reflection
            Subtractive Distillation
            Numerical Reflection: 20
            Division Distillation
            Ceiling Purification
            $print(Using main hand durability)
        )(
            // main hand max duability is 0
            Bookkeeper's Gambit: v
        )
    )()
)

// off hand
$if(
    Flock's Reflection
    Augur's Purification
)()(
    $if(
        Mind's Reflection
        Shieldbearer's Purification
        Length Purification
        Augur's Purification
    )(
        // off hand is not empty
        $if(
            Mind's Reflection
            Accessory Purification
            Fragility Purification

            Gemini Decomposition
            Augur's Purification
        )(
            // off hand max duability is non 0
            Menderbug's Reflection II
            Subtractive Distillation
            Numerical Reflection: 20
            Division Distillation
            Ceiling Purification
            $print(Using off hand durability)
        )(
            // off hand max duability is 0
            Bookkeeper's Gambit: v
        )
    )()
)

// no durability items
$if(
    Flock's Reflection
    Augur's Purification
)()(
    $print(Using default amount of media)
    Numerical Reflection: 640
)

// print amount of media
Reveal



// get amount
Numerical Reflection: 7.5
Division Distillation
Ceiling Purification


// get caster foot pos
Mind's Reflection
Compass' Purification II

// get mote

$publicAmethystMote
Gemini Decomposition
Length Purification
Augur's Purification
$fastif()(
    $print(No pseudoamethyst shards in public mote nexus)
    Janus' Gambit
)

// get items from mote
Reflecting Gambit
Return Item

// repair using amethyst on the ground
$foreach(
    // scan for items on ground
    Mind's Reflection
    Compass' Purification II
    Numerical Reflection: 1
    Zone Distillation: Item
)(
    $if(
        Gemini Decomposition
        Sorter's Purification
        {@item_type(oneironaut:pseudoamethyst_shard, true)} >>
        Equality Distillation
    )(
        {
            // handle repair if is amethyst shard
            Induce Mending
        }
        Athena's Gambit
        Bookkeeper's Gambit: v
        {
            // handle refill if is amethyst shard
            Recharge Item
        }
        Athena's Gambit
        Bookkeeper's Gambit: v
    )()
)
Bookkeeper's Gambit: v


// return left amethyst
$foreach(
    // scan for items on ground
    Mind's Reflection
    Compass' Purification II
    Numerical Reflection: 1
    Zone Distillation: Item
)(
    $if(
        Gemini Decomposition
        Sorter's Purification
        {@item_type(oneironaut:pseudoamethyst_shard, true)} >>
        Equality Distillation
    )(
        $publicAmethystMote
        Mediafy Item
    )()
)
Bookkeeper's Gambit: v
