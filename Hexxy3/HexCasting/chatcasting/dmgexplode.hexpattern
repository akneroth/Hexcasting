#git(/HexCasting/libs/base.hexpattern)
Gemini Decomposition
Length Purification
Numerical Reflection: 1
Maximus Distillation II
$fastif()(
    $print(Usage: dmgexplode <strength>)
    Janus' Gambit
)
Speaker's Decomposition
Input Purification
Gemini Decomposition
Augur's Purification
$fastif()(
    $print(Explode strenght must be a number)
    Janus' Gambit
)


$target_entity
Gemini Decomposition
Augur's Purification
$fastif(
    Compass' Purification
    Mind's Reflection
    Compass' Purification
    Subtractive Distillation
    Length Purification
)(
    Bookkeeper's Gambit: v
    Numerical Reflection: 100
)


$target_block_face
$target_block
Gemini Decomposition
Augur's Purification
$fastif(
    Additive Distillation
    Mind's Reflection
    Compass' Purification
    Subtractive Distillation
    Length Purification
)(
    Bookkeeper's Gambit: vv
    Numerical Reflection: 100
)

// n,n
Dioscuri Gambit
Equality Distillation
// n,n,b
Jester's Gambit
// n,b,n
Gemini Decomposition
Numerical Reflection: 100
// n,b,n,n,100
Equality Distillation
// n,b,n,b
Bubbling Gambit
// n,n,b,b
Conjunction Distillation
$fastif(
    $print(No target or target too far.)
    Janus' Gambit
)(
    Minimus Distillation II
    $fastif(
        $target_entity
        Compass' Purification
    )(
        $target_block_on_block_face
    )
)
// stack: [], expl_str, vector
Gemini Decomposition
$iotatype
{@iota_type(hexcasting:vec3)}>>
Equality Distillation
$fastif(
    Gemini Decomposition
    Create Water

    Gemini Decomposition
    Reflecting Gambit
    Explosion
    Gemini Decomposition
    Conjure Block
    Break Block
)()

