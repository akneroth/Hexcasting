// vector, pattern, any, bool -> 

#def(check_akasha)(
    // vector, pattern ->
    Akasha's Distillation
    Nullary Reflection
    Inequality Distillation
    {
        {
            @str(Hex with this pattern already exists)
        }
        Reveal
        Janus' Gambit
    }
    {

    }
    Augur's Exaltation
)

Flock's Reflection
Flock's Gambit
@str(bubbled property)
Jester's Gambit
Schr√∂dinger's Gambit



// check if there are 4 params
@str(bubbled property)
Observation Purification
Length Purification
Numerical Reflection: 4
Equality Distillation
{
    // check if force is true
    @str(bubbled property)
    Observation Purification
    Flock's Disintegration
    Bookkeeper's Gambit: vvv-
    True Reflection
    Equality Distillation
    {
        // allow to overwrite the previous spell with this pattern
    }
    {
        // stop hex if pattern exists
        @str(bubbled property)
        Observation Purification
        Flock's Disintegration
        Bookkeeper's Gambit: --vv
        $check_akasha
    }
    Augur's Exaltation
    
    // cleanup force param
    @str(bubbled property)
    Observation Purification
    Flock's Disintegration
    Bookkeeper's Gambit: ---v
    Numerical Reflection: 4
    Flock's Gambit
}
{
    // if no bool is passed for forcing overwrite, stop hex if pattern exists
    @str(bubbled property)
    Observation Purification
    Flock's Disintegration
    Bookkeeper's Gambit: --v
    $check_akasha
}
Augur's Exaltation

@str(bubbled property)
Observation Purification

// add to akasha
Gemini Decomposition
Flock's Disintegration
Akasha's Gambit

// take from akasha to everbook
Gemini Decomposition
Flock's Disintegration
Bookkeeper's Gambit: --v
Elysium's Gambit

// make natural
Flock's Disintegration
Bookkeeper's Gambit: v-v
Brigh's Gambit